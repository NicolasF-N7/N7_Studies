--Specification du module registre. Il utilise un tableau de listes du module liste_chainee pour y insérer des données de type T_Donnée.
--Ce module est réalisé avec la méthode du hachage.

with Liste_Chainee;
with Calendar; use Calendar;
with Ada.Text_IO; use Ada.Text_IO;

generic
    Capacite: Integer;--Le nombre de listes que contiendra le tableau / table de hashage

package Registre is
    
    type T_Sexe is (Homme, Femme);
    
    type T_Donnee is
        record
            Id : Integer;
            Prenom: String(1..30);
            Nom: String(1..30);
            Date_Naissance: Time;
            Sexe: T_Sexe;
        end record;

    --Instanciation du package Liste_Chainee sur le type T_Donnee: Liste de T_Donnee representant les individus
    package Liste_Donnee is 
            new liste_chainee (T_Element => T_Donnee);
    use Liste_Donnee;
    
    type T_Registre is limited private; --Type registre.
    
    --Initialiser un registre.
    procedure Initialiser_Registre (Registre : out T_Registre) with
            Post => Est_Registre_Vide(Registre);
    
    --Savoir si un registre ne contient que des listes vides.
    function Est_Registre_Vide (Registre : in T_Registre) return Boolean;
    
    -- Savoir si une donnée est présente dans un registre.
    function Est_Present_Registre (Registre : in T_Registre; Donnee : in T_Donnee) return boolean;
    
    --Connaitre les données de toutes les personnes de même nom, prenom.
    function Connaitre_Donnees_Homonymes (Registre : in T_Registre; Nom : in String; Prenom : in String) return T_Liste;
    
    --Ajouter une donnée dans un registre en fonction du modulo capacite.
    procedure Ajouter_Registre (Registre : in out T_Registre; Donnee : in T_Donnee) with
            pre => not(Est_Present_Registre (Registre, Donnee)),
            post => Est_Present_Registre (Registre, Donnee);
    
    -- Connaitre la donnée a partir d'un id.
    function Donnee (Registre : in T_Registre; id : in Integer) return T_Donnee;
    
    -- Modifier un element dans le registrer.
    procedure Modifier_Registre (Registre : in out T_Registre; Donnee : in T_Donnee) with 
            post => Est_Present_Registre(Registre, Donnee);
    
    -- Connaitre la taille d'un registre.
    function Taille_Registre (Registre : in T_Registre) return Integer;
    
    -- Supprimer une donnée dans un registre.
    procedure Supprimer_Registre(Registre : in out T_Registre; Donnee : in T_Donnee) with 
            pre => Est_Present_Registre (Registre, Donnee),
            post => not(Est_Present_Registre (Registre, Donnee));
    
            
    --Detruire un registre (toutes les listes sont de tailles vides).
    procedure Detruire2 (Registre : in out T_Registre) with
            post => Est_Registre_Vide(Registre);
 
private
    
    type T_Registre is array (1..Capacite) of T_Liste;
    
end Registre;
